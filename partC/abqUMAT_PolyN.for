!DIR$ FREEFORM
!CC*******************************************************************************
!C  UMAT: developed by S.C. Soare. 
!C  IT USES ISOTROPIC ELASTICITY COUPLED WITH AN ANISOTROPIC YIELD FUNCTION. 
!C  THE SUBROUTINE IS DESIGNED FOR PLANE (2D) STRESS STATES. 
!C  IT IS BASED ON THE FULLY IMPLICIT RETURN MAPPING ALGORITHM (with quadratic line search) 
!C  ONE STATE VARIABLE : HARDENING PARAMETER (THE EQUIVALENT PLASTIC STRAIN).
!C  IT IS ASSUMED THAT THE USER HAS DEFINED (USING THE *ORIENTATION OPTION IN ABAQUS)
!C  A LOCAL COORDINATE SYSTEM THAT ROTATES WITH THE MATERIAL (SO THAT DEROT IS ALWAYS
!C  EQUAL WITH THE IDENTITY MATRIX).
!C*****************************************************************************
!C The vector PROPS(NPROPS) contains the material properties as defined in the 
!C  *MATERIAL=USER option in ABAQUS in the following order
!C  PROPS(1) = EMOD  (Elasticity: Young modulus)
!C  PROPS(2) = MU  (Elasticity: Poisson ratio)
!C  Hardening laws defined: 
!C  Swift (power-)law: sigma^bar = a*(b + ep^bar)**c
!C  Voce (exp-)law: sigma^bar = a - b*exp(-c*ep^bar)  (default)
!C  Read further naming/renaming convention in the HARDENING section of this code 
!C  (more specific hardening laws can be implemented in the same section) 
!C  PROPS(3) = a 
!C  PROPS(4) = b
!C  PROPS(5) = c
!C  PROPS(6),...,PROPS(NPROPS): PARAMETERS OF YIELD FUNCTION
!C  Yield func implemented: PolyN
!C  Note: The first two parameters are the degree and number of coefficients
!C!************************************************************************************** 

!CCCC-----NOTE: the UMAT interface may vary with the ABAQUS version 	
!C	SUBROUTINE UMAT(STRESS,STATEV,DDSDDE,SSE,SPD,SCD, &
!C      RPL,DDSDDT,DRPLDE,DRPLDT, &
!C      STRAN,DSTRAN,TIME,DTIME,TEMP,DTEMP,PREDEF,DPRED,CMNAME, &
!C      NDI,NSHR,NTENS,NSTATV,PROPS,NPROPS,COORDS,DROTT,PNEWDT, &
!C      CELENT,DFGRD0,DFGRD1,NOEL,NPT,LAYER,KSPT,KSTEP,KINC)

    SUBROUTINE UMAT(STRESS,STATEV,DDSDDE,SSE,SPD,SCD, &
      RPL,DDSDDT,DRPLDE,DRPLDT, &
      STRAN,DSTRAN,TIME,DTIME,TEMP,DTEMP,PREDEF,DPRED,CMNAME, &
      NDI,NSHR,NTENS,NSTATV,PROPS,NPROPS,COORDS,DROT,PNEWDT, &
      CELENT,DFGRD0,DFGRD1,NOEL,NPT,LAYER,KSPT,JSTEP,KINC)

!CCC---NOTE: the INCLUDE directive enforces implicit casting in conflict with 'IMPLICIT NONE'
!CC          use 'IMPLICIT NONE' in the testing/implementation phase and then comment it out 
!    IMPLICIT NONE      	
	INCLUDE 'ABA_PARAM.INC'
!C    INTEGER, PARAMETER :: PREC =  SELECTED_REAL_KIND(15,307)
    INTEGER, PARAMETER :: PREC = 8
!C******************************************************************************
!C  VARIABLES REQUIRED BY ABAQUS (THE ARGUMENTS OF THE SUBROUTINE)
!C  FOR A DESCRIPTION OF THE LIST OF VARIABLES SEE ABAQUS MANUAL (VOL. VI)

!C    !!!CHARACTER(80)::  CMNAME
	CHARACTER*80 CMNAME
    REAL(PREC)::SSE,SPD,SCD,RPL,DRPLDT,DTIME,TEMP,DTEMP,PNEWDT,CELENT
    INTEGER::NDI,NSHR,NTENS,NSTATV,NPROPS,NOEL,NPT,LAYER,KSPT,KINC
    REAL(PREC),DIMENSION(NTENS):: STRESS,DDSDDT,DRPLDE,STRAN,DSTRAN
    REAL(PREC),DIMENSION (NTENS, NTENS) :: DDSDDE 
    REAL(PREC),DIMENSION(NSTATV) :: STATEV
    REAL(PREC),DIMENSION(NPROPS) :: PROPS
    REAL(PREC),DIMENSION(3,3) :: DFGRD0, DFGRD1, DROT
    REAL(PREC),DIMENSION(3) :: COORDS
    REAL(PREC),DIMENSION(2) :: TIME
    REAL(PREC),DIMENSION(1) :: PREDEF, DPRED
    INTEGER,DIMENSION(4)::JSTEP
		
!C    CHARACTER*80 CMNAME
!C	INTEGER::NDI,NSHR,NTENS,NSTATV,NPROPS,NOEL,NPT,LAYER,KSPT,KINC
!C	REAL(PREC)::SSE,SPD,SCD,RPL,DRPLDT,DTIME,TEMP,DTEMP,PNEWDT,CELENT
!C    REAL(PREC),DIMENSION::STRESS(NTENS),STATEV(NSTATV),DDSDDE(NTENS,NTENS), &
!C	DDSDDT(NTENS),DRPLDE(NTENS),STRAN(NTENS),DSTRAN(NTENS), &
!C	TIME(2),PREDEF(1),DPRED(1),PROPS(NPROPS), &
!C	COORDS(3),DROT(3,3),DFGRD0(3,3),DFGRD1(3,3)
!C	INTEGER,DIMENSION::JSTEP(4)	
	
!C*******************************************************************************
!C  INTERNAL VARIABLES OF THE SUBROUTINE
      
!C  VECTOR O MATERIAL PARAMETERS
    INTEGER::NKMAT
	REAL(PREC),ALLOCATABLE, DIMENSION(:)::KMATERIALX,KMATERIALY
    
    REAL(PREC), PARAMETER::ZERO=0.0D0

!C  elastic constants
    REAL(PREC) :: EMOD, ENU
!C  COMPLIANCE TENSOR
    REAL(PREC),DIMENSION(NTENS,NTENS)::SCOMP

!C  HARDENING PARAMETERS
    REAL(PREC)::AA,BB,CC
!C  HARDENING VALUES
    REAL(PREC):: HF, HPF

!C  STRESS TENSOR AND ITS INCREMENTS
    REAL(PREC),DIMENSION(NTENS)::SIGMA, DSIGMA, D2SIGMA

!C  EQUIVALENT PLASTIC STRAIN AND ITS INCREMENTS
    REAL(PREC):: EPBAR, DEPBAR, D2EPBAR

!C  YIELD FUNCTION VALUE, GRADIENT AND HESSIAN
    REAL(PREC):: YF
	REAL(PREC),DIMENSION(NTENS)::GYF
	REAL(PREC),DIMENSION(NTENS,NTENS)::HYF

!C  CONVERGENCE TOLERANCES
!    REAL(PREC),PARAMETER::TOL1=1.0E-006, TOL2=1.0E-008
	REAL(PREC),PARAMETER::TOL1=1.0E-007

!C  TEMPORARY HOLDERS
    REAL(PREC)::TT, TTA, TTB, ZALPHA, F1, FZERO, TDEPBAR
	REAL(PREC),DIMENSION(NTENS)::YVECTOR, F2, TDSIGMA
	REAL(PREC),DIMENSION(NTENS,NTENS)::XIMAT
	REAL(PREC),DIMENSION(6)::BV
	REAL(PREC),DIMENSION(3)::ZZ

!C  LOOP COUNTERS
    INTEGER::K1,K2,NRK

!C  NEWTON-RAPHSON MAXIMUM NUMBER OF ITERATIONS
    INTEGER,PARAMETER:: NRMAX=100  
	
!C PolyN interface variables 
    INTEGER::DEGREE,NCOEFF,NMON	

!C*****************************************************
	EMOD = PROPS(1)
    ENU = PROPS(2)
    AA = PROPS(3)
    BB = PROPS(4)
    CC = PROPS(5)
	DEGREE = INT(PROPS(6))
	NCOEFF = INT(PROPS(7))
	NKMAT = (NPROPS - 7)/2
    NMON=INT(DEGREE/2)
	
	ALLOCATE (KMATERIALX(NKMAT))
	KMATERIALX = PROPS(8:7+NKMAT)
	ALLOCATE (KMATERIALY(NKMAT))
	KMATERIALY = PROPS(8+NKMAT:)
			
!C!********************************************
!C RECOVER THE EQUIVALENT PLASTIC STRAIN AT THE BEGINING OF THE INCREMENT
    EPBAR = STATEV(1)
      
!C!********************************************
!C INITIALIZE THE STIFFNESS TENSOR (IT WILL BE STORED IN DDSDDE)
    DDSDDE = ZERO

!C ELASTIC PROPERTIES (plane stress)
	TT = 0.5D0*EMOD/(1.0D0+ENU)
	TTA=EMOD/(1.0D0-ENU*ENU)
	TTB=ENU*TTA

    DDSDDE(1,1) = TTA
    DDSDDE(2,1) = TTB
    DDSDDE(1,2) = TTB
    DDSDDE(2,2) = TTA
	DDSDDE(3,3) = TT

!C COMPUTE THE TRIAL STRESS : SIGMA_{N+1} = SIGMA_{N} + C[DELTA_EPSILON]
    DO K1=1,NTENS,1
        TT=DDSDDE(K1,1)*DSTRAN(1)+DDSDDE(K1,2)*DSTRAN(2)+DDSDDE(K1,3)*DSTRAN(3)
	    DSIGMA(K1) = TT
	    SIGMA(K1)=STRESS(K1)+TT
    END DO

!C CHECK YIELDING CONDITION
    CALL KHARD(HF,HPF,EPBAR,AA,BB,CC)
    CALL YFUNCTION(SIGMA,NTENS,YF,KMATERIALX,KMATERIALY,NKMAT,DEGREE,NCOEFF,NMON)

!C  ELASTIC STEP :  UPDATE STRESS
	IF (YF <= HF) THEN
	    STRESS = SIGMA
!C  DDSDDE HAS BEEN DEFINED ABOVE
!C  THE EQUIVALENT PLASTIC STRAIN, STATEV(1), REMAINS UNCHANGED
        DEALLOCATE(KMATERIALX)
        DEALLOCATE(KMATERIALY)		
        RETURN
	END IF

!C***********************************************
!C MAIN LOOP : RETURN MAPPING ALGORITHM

!C  DEFINE COMPLIANCE (note that it outputs ENGINEERING shears)
	TTA=1.0D0/EMOD
	TTB=-ENU/EMOD 
	SCOMP(1,1)=TTA
	SCOMP(2,1)=TTB
	SCOMP(3,1)=ZERO
	SCOMP(1,2)=TTB
	SCOMP(2,2)=TTA
	SCOMP(3,2)=ZERO
	SCOMP(1,3)=ZERO
	SCOMP(2,3)=ZERO
	SCOMP(3,3)=2.0D0*(1.0D0+ENU)/EMOD

!C    FIRST Newton-Raphson step (no Hessian required)
!C**************************************************************      
!C  DEPBAR=ZERO

	CALL GYFUNCTION(SIGMA,NTENS,YF,GYF,KMATERIALX,KMATERIALY,NKMAT,DEGREE,NCOEFF,NMON)
	F1=YF-HF

!C  ASSEMBLE XIMAT MATRIX AND Y-VECTOR
    DO K1=1,NTENS,1
	    YVECTOR(K1)=-F1*GYF(K1)
	    DO K2=K1,NTENS,1
	        TT=HPF*SCOMP(K1,K2)+GYF(K1)*GYF(K2)
	        XIMAT(K1,K2)=TT
	        XIMAT(K2,K1)=TT
	    END DO
	END DO

!C  SOLVE FOR STRESS NR-INCREMENT
    BV(1)=DSQRT(XIMAT(1,1))
	BV(2)=XIMAT(2,1)/BV(1)
	BV(3)=XIMAT(3,1)/BV(1)
	BV(4)=DSQRT(XIMAT(2,2)-BV(2)*BV(2))
	BV(5)=(XIMAT(3,2)-BV(2)*BV(3))/BV(4)
	BV(6)=DSQRT(XIMAT(3,3)-(BV(3)*BV(3)+BV(5)*BV(5)))
	ZZ(1)=YVECTOR(1)/BV(1)
	ZZ(2)=(YVECTOR(2)-BV(2)*ZZ(1))/BV(4)
	ZZ(3)=(YVECTOR(3)-(BV(3)*ZZ(1)+BV(5)*ZZ(2)))/BV(6)
	D2SIGMA(3)=ZZ(3)/BV(6)
	D2SIGMA(2)=(ZZ(2)-BV(5)*D2SIGMA(3))/BV(4)
	D2SIGMA(1)=(ZZ(1)-(BV(2)*D2SIGMA(2)+BV(3)*D2SIGMA(3)))/BV(1)

!C  CALCULATE EQUIVALENT PLASTIC STRAIN NR-INCREMENT 
    D2EPBAR=F1
	DO K1=1,NTENS,1
	    D2EPBAR=D2EPBAR+GYF(K1)*D2SIGMA(K1)
	END DO
	D2EPBAR=D2EPBAR/HPF
    
!C  DO LINE SEARCH (along the full NR-step)
    TDEPBAR=D2EPBAR
	TDSIGMA=DSIGMA+D2SIGMA	
    FZERO=F1
    CALL LSEARCH(NTENS,STRESS,TDSIGMA,DSTRAN,EPBAR,TDEPBAR,FZERO, &
	             SCOMP,KMATERIALX,KMATERIALY,NKMAT,AA,BB,CC,ZALPHA,DEGREE,NCOEFF,NMON)

!C  UPDATE
    DEPBAR=ZALPHA*D2EPBAR
	DSIGMA=DSIGMA+ZALPHA*D2SIGMA
	
!C    THE REST OF N-R ITERATIONS
!C******************************************************	     
    DO NRK=1,NRMAX,1

!C      CALCULATE NEW VALUES ASSOCIATED WITH NEW STATE
        CALL KHARD(HF,HPF,EPBAR+DEPBAR,AA,BB,CC)
	    SIGMA=STRESS+DSIGMA
	    CALL HYFUNCTION(SIGMA,NTENS,YF,GYF,HYF,KMATERIALX,KMATERIALY,NKMAT,DEGREE,NCOEFF,NMON)
		
	    F1=YF-HF
	    FZERO=F1*F1
	    DO K1=1,NTENS,1
	        TT=DEPBAR*GYF(K1)-DSTRAN(K1)
	        DO K2=1,NTENS,1
	            TT=TT+SCOMP(K1,K2)*DSIGMA(K2)
	        END DO
	        F2(K1)=TT
	        FZERO=FZERO+TT*TT
	    END DO
        FZERO=DSQRT(FZERO)
!C      CHECK TOLERANCES
!C        IF ((DABS(F1)<TOL1).AND.(DSQRT(TTB)<TOL2)) EXIT
        IF(FZERO<TOL1) EXIT

!C      ASSEMBLE XIMAT MATRIX AND Y-VECTOR
        DO K1=1,NTENS,1
	        YVECTOR(K1)=-(F1*GYF(K1)+HPF*F2(K1))
	        DO K2=K1,NTENS,1
	            TT=HPF*(SCOMP(K1,K2)+DEPBAR*HYF(K1,K2))+GYF(K1)*GYF(K2)
	            XIMAT(K1,K2)=TT
                XIMAT(K2,K1)=TT
            END DO
	    END DO

!C      SOLVE FOR STRESS NR-INCREMENT
        BV(1)=DSQRT(XIMAT(1,1))
		BV(2)=XIMAT(2,1)/BV(1)
		BV(3)=XIMAT(3,1)/BV(1)
		BV(4)=DSQRT(XIMAT(2,2)-BV(2)*BV(2))
		BV(5)=(XIMAT(3,2)-BV(2)*BV(3))/BV(4)
		BV(6)=DSQRT(XIMAT(3,3)-(BV(3)*BV(3)+BV(5)*BV(5)))
		ZZ(1)=YVECTOR(1)/BV(1)
		ZZ(2)=(YVECTOR(2)-BV(2)*ZZ(1))/BV(4)
		ZZ(3)=(YVECTOR(3)-(BV(3)*ZZ(1)+BV(5)*ZZ(2)))/BV(6)
		D2SIGMA(3)=ZZ(3)/BV(6)
		D2SIGMA(2)=(ZZ(2)-BV(5)*D2SIGMA(3))/BV(4)
		D2SIGMA(1)=(ZZ(1)-(BV(2)*D2SIGMA(2)+BV(3)*D2SIGMA(3)))/BV(1)

!C      CALCULATE EQUIVALENT PLASTIC STRAIN NR-INCREMENT 
        D2EPBAR=F1
	    DO K1=1,NTENS,1
	        D2EPBAR=D2EPBAR+GYF(K1)*D2SIGMA(K1)
	    END DO
	    D2EPBAR=D2EPBAR/HPF

!C      DO LINE SEARCH
        TDEPBAR=DEPBAR+D2EPBAR
	    TDSIGMA=DSIGMA+D2SIGMA
        CALL LSEARCH(NTENS,STRESS,TDSIGMA,DSTRAN,EPBAR,TDEPBAR,FZERO, &
	             SCOMP,KMATERIALX,KMATERIALY,NKMAT,AA,BB,CC,ZALPHA,DEGREE,NCOEFF,NMON)

!C      UPDATE
        DEPBAR=DEPBAR+ZALPHA*D2EPBAR
	    DSIGMA=DSIGMA+ZALPHA*D2SIGMA

	END DO !!! END OF NEWTON-RAPHSON ITERATIONS
        
!C  UPDATE STATE VARIABLE
    STATEV(1)=EPBAR+DEPBAR

!C  UPDATE STRESS
    STRESS = STRESS+DSIGMA

!C************************************** COMPUTE TANGENT MODULUS: DDSDDE

!C  COMPUTE XIMAT MATRIX 
    DO K1=1,NTENS,1
	    DO K2=K1,NTENS,1
	        TT=SCOMP(K1,K2)+DEPBAR*HYF(K1,K2)
	        XIMAT(K1,K2)=TT
	        XIMAT(K2,K1)=TT
	    END DO
	END DO

!C  INVERT XIMAT AND STORE XIMAT^(-1) INTO SCOMP (NO LONGER NEEDED)
    BV(1)=DSQRT(XIMAT(1,1))
	BV(2)=XIMAT(2,1)/BV(1)
	BV(3)=XIMAT(3,1)/BV(1)
	BV(4)=DSQRT(XIMAT(2,2)-BV(2)*BV(2))
	BV(5)=(XIMAT(3,2)-BV(2)*BV(3))/BV(4)
	BV(6)=DSQRT(XIMAT(3,3)-(BV(3)*BV(3)+BV(5)*BV(5)))
	
	ZZ(1)=1.0D0/BV(1)
	ZZ(2)=-(BV(2)*ZZ(1))/BV(4)
	ZZ(3)=-(BV(3)*ZZ(1)+BV(5)*ZZ(2))/BV(6)
	SCOMP(3,1)=ZZ(3)/BV(6)
	SCOMP(2,1)=(ZZ(2)-BV(5)*SCOMP(3,1))/BV(4)
	SCOMP(1,1)=(ZZ(1)-(BV(2)*SCOMP(2,1)+BV(3)*SCOMP(3,1)))/BV(1)

!	ZZ(1)=ZERO
	ZZ(2)=1.0D0/BV(4)
	ZZ(3)=-(BV(5)*ZZ(2))/BV(6)
	SCOMP(3,2)=ZZ(3)/BV(6)
	SCOMP(2,2)=(ZZ(2)-BV(5)*SCOMP(3,2))/BV(4)
	SCOMP(1,2)=SCOMP(2,1)
	
!	ZZ(1)=ZERO
!	ZZ(2)=ZERO
	ZZ(3)=1.0D0/BV(6)
	SCOMP(3,3)=ZZ(3)/BV(6)
	SCOMP(2,3)=SCOMP(3,2)
	SCOMP(1,3)=SCOMP(3,1)
	
!C  CALCULATE  SCOMP[GYF] AND STORE IT INTO DSIGMA
!C  DSIGMA=(/ZERO,ZERO,ZERO/)
	DSIGMA=ZERO
    DO K1=1,NTENS,1
	    DO K2=1,NTENS,1
	        DSIGMA(K2)=DSIGMA(K2)+SCOMP(K2,K1)*GYF(K1)
	    END DO
	END DO

!C  CALCULATE 1/K
    TT=HPF
	DO K1=1,NTENS,1
	    TT=TT+GYF(K1)*DSIGMA(K1)
	END DO

!C  UPDATE DDSDDE
    DO K1=1,NTENS,1
	    DO K2=K1,NTENS,1
	        TTB=SCOMP(K1,K2)-DSIGMA(K1)*DSIGMA(K2)/TT
	        DDSDDE(K1,K2)=TTB
	        DDSDDE(K2,K1)=TTB
	    END DO
	END DO
	DO K1=1,NTENS,1
	    DDSDDE(K1,K1)=SCOMP(K1,K1)-DSIGMA(K1)*DSIGMA(K1)/TT
	END DO
	DDSDDE(2,3)=SCOMP(2,3)-DSIGMA(2)*DSIGMA(3)/TT
	DDSDDE(1,3)=SCOMP(1,3)-DSIGMA(1)*DSIGMA(3)/TT
	DDSDDE(1,2)=SCOMP(1,2)-DSIGMA(1)*DSIGMA(2)/TT
	DDSDDE(2,1)=DDSDDE(1,2)
	DDSDDE(3,1)=DDSDDE(1,3)
	DDSDDE(3,2)=DDSDDE(2,3)

    DEALLOCATE(KMATERIALX)
    DEALLOCATE(KMATERIALY)	
    RETURN
    END SUBROUTINE  UMAT


!C**************************HARDENING***************************
!C*****NOTE:
!C*****THIS UMAT IDENTIFIES THE HARDENING SUB BY THE NAME 'KHARD'
!C*****(DEACTIVATE THE OTHER BY RENAMING)
     	
!C****: Swift (Power hardening law)
    SUBROUTINE   KHARD(HF,HPF,EPBAR,AAZ,BBZ,CCZ)
!C      COMPUTES THE HARDENING AND ITS DERIVATIVE
    
    IMPLICIT NONE
    INTEGER, PARAMETER :: PREC = 8
    REAL(PREC) :: HF, HPF, EPBAR, AAZ, BBZ, CCZ
    HF  = AAZ*((BBZ+EPBAR)**CCZ)
    HPF =  (CCZ/(BBZ+EPBAR))*HF
    RETURN
    END SUBROUTINE  KHARD

!C****: Voce (Exponential hardening law)
    SUBROUTINE  vcKHARD(HF,HPF,EPBAR,AAZ,BBZ,CCZ)
!C      COMPUTES THE HARDENING AND ITS DERIVATIVE
    
    IMPLICIT NONE
    INTEGER, PARAMETER :: PREC = 8
    REAL(PREC) :: HF, HPF, EPBAR, AAZ, BBZ, CCZ
    HF=BBZ/EXP(CCZ*EPBAR)
	HPF = CCZ*HF
    HF  = AAZ-HF
    RETURN
    END SUBROUTINE  vcKHARD

!C**********************************************************
	SUBROUTINE LSEARCH(NTENS,STRESS,TDSIGMA,DSTRAN,EPBAR,TDEPBAR,FZERO, &
	                   SCOMP,KMATERIALX,KMATERIALY,NKMAT,AAZ,BBZ,CCZ,ZALPHA,DEGREE,NCOEFF,NMON) 

	IMPLICIT NONE
	INTEGER,PARAMETER::PREC=8

    INTEGER::NTENS, NKMAT,DEGREE,NCOEFF,NMON
	REAL(PREC),DIMENSION(NTENS)::STRESS,TDSIGMA,DSTRAN
	REAL(PREC),DIMENSION(NTENS,NTENS)::SCOMP
	REAL(PREC)::EPBAR,TDEPBAR,FZERO,AAZ,BBZ,CCZ,ZALPHA
	REAL(PREC),DIMENSION(NKMAT)::KMATERIALX,KMATERIALY

!C     INTERNAL VARIABLES
    REAL(PREC),DIMENSION(NTENS)::TSIGMA,GYF  
	REAL(PREC)::HF,HPF,TEPBAR,YF,TT,FONE
	INTEGER::KK,JJ

    TSIGMA=STRESS+TDSIGMA
	TEPBAR=EPBAR+TDEPBAR
	
	CALL KHARD(HF,HPF,TEPBAR,AAZ,BBZ,CCZ)
	CALL GYFUNCTION(TSIGMA,NTENS,YF,GYF,KMATERIALX,KMATERIALY,NKMAT,DEGREE,NCOEFF,NMON)
	
    FONE=(YF-HF)
	FONE=FONE*FONE
	DO KK=1,NTENS,1
	    TT=TDEPBAR*GYF(KK)-DSTRAN(KK)
	    DO JJ=1,NTENS,1
	        TT=TT+SCOMP(KK,JJ)*TDSIGMA(JJ)
	    END DO
	FONE=FONE+TT*TT
	END DO
	FONE=DSQRT(FONE)
	ZALPHA=1.0D0
	IF(FONE<=0.5D0*FZERO) RETURN	
	
!!	ZALPHA=0.75D0*(FZERO/(2.0D0*FONE))
    ZALPHA=0.375D0*(FZERO/FONE)	
    RETURN
    END SUBROUTINE LSEARCH	
!C*********************************************************************************

!CCCCCCC********** YIELD FUNCTION CALCULATIONS ***********************************
!CCCCCCC NOTE:   YFUNCTION RETURNS JUST YIELD FUNCTION VALUE
!CCCCCCC        GYFUNCTION RETURNS YIELD FUNCTION VALUE AND GRADIENT
!CCCCCCC        HYFUNCTION RETURNS YIELD FUNCTION VALUE, GRADIENT AND HESSIAN
!C
!CCCCCC************** PolyN YIELD FUNCTION: 
!CCCCCC************** 1) ORTHOTROPIC SYMMETRY 
!CCCCCC************** 2) HOMOGENEITY DEGREE = N (any degree)
!CCCCCC************** 3) PLANE STRESS

    SUBROUTINE YFUNCTION(SIGMA,NTENS,YF,KMATERIALX,KMATERIALY,NKMAT,DEGREE,NCOEFF,NMON)
    IMPLICIT NONE
	INTEGER,PARAMETER::PREC=8

    INTEGER::NTENS,NKMAT,DEGREE,NCOEFF,NMON
	REAL(PREC),DIMENSION(NTENS)::SIGMA
	REAL(PREC),DIMENSION(NKMAT)::KMATERIALX,KMATERIALY,KMATERIAL
	REAL(PREC)::YF,ZZTT,ZZRHO,ZZGAMM,MSX,MSY
	REAL(PREC),PARAMETER::ZTOL=1.0E-007
	REAL(PREC)::BB
	INTEGER::JJ,KKO,MM
	
	YF=KMATERIALX(NCOEFF)
	MSX=DABS(SIGMA(1))
	MSY=DABS(SIGMA(2))
	IF(MSX+MSY<ZTOL) THEN 
	    YF=YF*DABS(SIGMA(3))
		RETURN
    ELSE
        IF(MSX>=MSY) THEN
		    ZZTT=SIGMA(1)
		    ZZRHO=SIGMA(2)/SIGMA(1)
		    ZZGAMM=SIGMA(3)/SIGMA(1)
	        KMATERIAL = KMATERIALX(1:)  
	    ELSE
		    ZZTT=SIGMA(2)
		    ZZRHO=SIGMA(1)/SIGMA(2)
		    ZZGAMM=SIGMA(3)/SIGMA(2)
	        KMATERIAL = KMATERIALY(1:)
        END IF
		ZZGAMM=ZZGAMM*ZZGAMM
    END IF
	
	KKO=NCOEFF
	JJ=NMON
	DO WHILE(JJ>0)
	    JJ=JJ-1
	    MM=DEGREE-2*JJ
		KKO=KKO-1
		BB=KMATERIAL(KKO)
	    DO WHILE(MM>0)
		    MM=MM-1
		    KKO=KKO-1
		    BB=KMATERIAL(KKO)+BB*ZZRHO
		END DO
		YF=BB+YF*ZZGAMM
	END DO
    
	YF=DABS(ZZTT)*(YF**(1.0D0/DBLE(DEGREE)))	
	RETURN
	END SUBROUTINE YFUNCTION

!CC************************************************************************
    SUBROUTINE GYFUNCTION(SIGMA,NTENS,YF,GYF,KMATERIALX,KMATERIALY,NKMAT,DEGREE,NCOEFF,NMON)
    IMPLICIT NONE
	INTEGER,PARAMETER::PREC=8

	INTEGER::NTENS,NKMAT,DEGREE,NCOEFF,NMON
	REAL(PREC),DIMENSION(NTENS)::SIGMA,GYF
	REAL(PREC),DIMENSION(NKMAT)::KMATERIALX,KMATERIALY,KMATERIAL
	REAL(PREC)::YF,ZZTT,ZZRHO,ZZGAMM,MSX,MSY
	REAL(PREC),PARAMETER::ZTOL=1.0E-007
	REAL(PREC)::ZYF,BB,D1BB,D2BB
	INTEGER::JJ,KKO,MM
    REAL(PREC),DIMENSION(NCOEFF)::VD1,VD2
	REAL(PREC),DIMENSION(NMON)::VD3(0:NMON-1)
	

	YF=KMATERIALX(NCOEFF)
    GYF=0.0D0
	GYF(3)=DBLE(NMON)*YF 
	MSX=DABS(SIGMA(1))
	MSY=DABS(SIGMA(2))
	IF(MSX+MSY<ZTOL) THEN 
	    YF=YF*DABS(SIGMA(3))
		GYF(3)=DSIGN(1.0D0,SIGMA(3))
		RETURN
    ELSE
        IF(MSX>=MSY) THEN
		    ZZTT=SIGMA(1)
		    ZZRHO=SIGMA(2)/SIGMA(1)
		    ZZGAMM=SIGMA(3)/SIGMA(1)
	        KMATERIAL = KMATERIALX(1:)  
	    ELSE
		    ZZTT=SIGMA(2)
		    ZZRHO=SIGMA(1)/SIGMA(2)
		    ZZGAMM=SIGMA(3)/SIGMA(2)
	        KMATERIAL = KMATERIALY(1:)
        END IF
		ZZGAMM=ZZGAMM*ZZGAMM
    END IF
	
	KKO=NCOEFF
	JJ=NMON
	MM=2*NCOEFF
	
	VD1=KMATERIAL(NCOEFF+1:MM)
	VD2=KMATERIAL(MM+1:MM+NCOEFF)
	VD3=0.0D0
	
	!GYF(1)=VD1(KKO)
	!GYF(2)=VD2(KKO)
	DO WHILE(JJ>0)
	    JJ=JJ-1
	    MM=DEGREE-2*JJ
		KKO=KKO-1
		BB=KMATERIAL(KKO)
		D1BB=VD1(KKO)
		D2BB=VD2(KKO)
	    DO WHILE(MM>0)
		    MM=MM-1
		    KKO=KKO-1
		    BB=KMATERIAL(KKO)+BB*ZZRHO
			D1BB=VD1(KKO)+D1BB*ZZRHO
            D2BB=VD2(KKO)+D2BB*ZZRHO
		END DO
		VD3(JJ)=JJ*BB
		YF=BB+YF*ZZGAMM
		GYF(1)=D1BB+GYF(1)*ZZGAMM
		GYF(2)=D2BB+GYF(2)*ZZGAMM
	END DO
	ZYF=YF**(1.0D0/DBLE(DEGREE))
	BB=DSIGN(1.0D0,ZZTT)*(ZYF/(DBLE(DEGREE)*YF))
	JJ=NMON
	DO WHILE(JJ>1)
	    JJ=JJ-1
        GYF(3)=VD3(JJ)+GYF(3)*ZZGAMM
	END DO
    GYF(1)=BB*GYF(1)
	GYF(2)=BB*GYF(2)
	GYF(3)=BB*((2.0D0*SIGMA(3))/ZZTT)*GYF(3)
	YF=DABS(ZZTT)*ZYF
	RETURN
	END SUBROUTINE GYFUNCTION

!CC***************************************************************************

    SUBROUTINE HYFUNCTION(SIGMA,NTENS,YF,GYF,HYF,KMATERIALX,KMATERIALY,NKMAT,DEGREE,NCOEFF,NMON)
    IMPLICIT NONE
	INTEGER,PARAMETER::PREC=8

	INTEGER::NTENS,NKMAT,DEGREE,NCOEFF,NMON
	REAL(PREC),DIMENSION(NTENS)::SIGMA,GYF
	REAL(PREC),DIMENSION(NTENS,NTENS)::HYF
	REAL(PREC),DIMENSION(NKMAT)::KMATERIALX,KMATERIALY,KMATERIAL
	REAL(PREC)::YF,ZZTT,ZZRHO,ZZGAMM,MSX,MSY
    REAL(PREC),PARAMETER::ZTOL=1.0E-007
	REAL(PREC)::ZYF,YVAL,Y2VAL,ATT,TMP,BB,D1BB,D2BB,D11BB,D22BB,D12BB
	INTEGER::JJ,KKO,MM
    REAL(PREC),DIMENSION(NCOEFF)::VD1,VD2,VD11,VD22,VD12
!    REAL(PREC),DIMENSION(NMON)::VD3,VD13,VD23,VD33
	REAL(PREC),DIMENSION(NMON)::VD3(0:NMON-1),VD13(0:NMON-1),VD23(0:NMON-1),VD33(0:NMON-1)
	
	GYF=0.0D0
	HYF=0.0D0
	VD3=0.0D0
	VD13=0.0D0
	VD23=0.0D0
	VD33=0.0D0
	
	YF=KMATERIALX(NCOEFF)
	GYF(3)=DBLE(NMON)*YF
    HYF(3,3)=DBLE(DEGREE-1)*GYF(3)	
	MSX=DABS(SIGMA(1))
	MSY=DABS(SIGMA(2))
	IF(MSX+MSY<ZTOL) THEN 
	    ATT=DABS(SIGMA(3))
		BB=ATT*DBLE(DEGREE)*YF**(1.0D0-1.0D0/DBLE(DEGREE))
		YF=YF*ATT
		GYF(3)=DSIGN(1.0D0,SIGMA(3))
		HYF(1,1)=(2.0D0*KMATERIAL(NCOEFF-3))/BB
		HYF(2,1)=KMATERIAL(NCOEFF-2)/BB
		HYF(1,2)=HYF(2,1)
		HYF(2,2)=(2.0D0*KMATERIAL(NCOEFF-1))/BB
		RETURN
    ELSE
        IF(MSX>=MSY) THEN
		    ZZTT=SIGMA(1)
		    ZZRHO=SIGMA(2)/SIGMA(1)
		    ZZGAMM=SIGMA(3)/SIGMA(1)
	        KMATERIAL = KMATERIALX(1:)  
	    ELSE
		    ZZTT=SIGMA(2)
		    ZZRHO=SIGMA(1)/SIGMA(2)
		    ZZGAMM=SIGMA(3)/SIGMA(2)
	        KMATERIAL = KMATERIALY(1:)
        END IF
		ZZGAMM=ZZGAMM*ZZGAMM
    END IF
	
	KKO=NCOEFF
	JJ=NMON
	MM=2*NCOEFF
	
	VD1=KMATERIAL(NCOEFF+1:MM)
	VD2=KMATERIAL(MM+1:MM+NCOEFF)
	MM=MM+NCOEFF
	VD11=KMATERIAL(MM+1:MM+NCOEFF)
	MM=MM+NCOEFF
	VD22=KMATERIAL(MM+1:MM+NCOEFF)
	MM=MM+NCOEFF
	VD12=KMATERIAL(MM+1:MM+NCOEFF)
	
	!GYF(1)=VD1(KKO)
	!GYF(2)=VD2(KKO)
	!HYF(1,1)=VD11(KKO)
	!HYF(2,1)=VD12(KKO)
	!HYF(2,2)=VD22(KKO)
	DO WHILE(JJ>0)
	    JJ=JJ-1
	    MM=DEGREE-2*JJ
		KKO=KKO-1
		BB=KMATERIAL(KKO)
		D1BB=VD1(KKO)
		D2BB=VD2(KKO)
		D11BB=VD11(KKO)
		D22BB=VD22(KKO)
		D12BB=VD12(KKO)
	    DO WHILE(MM>0)
		    MM=MM-1
		    KKO=KKO-1
		    BB=KMATERIAL(KKO)+BB*ZZRHO
			D1BB=VD1(KKO)+D1BB*ZZRHO
            D2BB=VD2(KKO)+D2BB*ZZRHO
			D11BB=VD11(KKO)+D11BB*ZZRHO
			D22BB=VD22(KKO)+D22BB*ZZRHO
			D12BB=VD12(KKO)+D12BB*ZZRHO
		END DO
		VD3(JJ)=JJ*BB
		VD13(JJ)=JJ*D1BB
        VD23(JJ)=JJ*D2BB
        VD33(JJ)=JJ*BB*(2*JJ-1)
		YF=BB+YF*ZZGAMM
		GYF(1)=D1BB+GYF(1)*ZZGAMM
		GYF(2)=D2BB+GYF(2)*ZZGAMM
		HYF(1,1)=D11BB+HYF(1,1)*ZZGAMM
		HYF(2,1)=D12BB+HYF(2,1)*ZZGAMM
		HYF(2,2)=D22BB+HYF(2,2)*ZZGAMM
	END DO
	ZYF=YF**(1.0D0/DBLE(DEGREE))
	YVAL=ZYF/(DBLE(DEGREE)*YF)
	Y2VAL=DBLE(DEGREE-1)/ZYF
	ATT=DABS(ZZTT)
	BB=DSIGN(1.0D0,ZZTT)*YVAL
	!HYF(1,3)=VD13(NMON)
	!HYF(2,3)=VD23(NMON)
	!HYF(3,3)=DBLE(DEGREE-1)*GYF(3)
	JJ=NMON
	DO WHILE(JJ>1)
	    JJ=JJ-1
        GYF(3)=VD3(JJ)+GYF(3)*ZZGAMM
		HYF(1,3)=VD13(JJ)+HYF(1,3)*ZZGAMM
		HYF(2,3)=VD23(JJ)+HYF(2,3)*ZZGAMM
		HYF(3,3)=VD33(JJ)+HYF(3,3)*ZZGAMM
	END DO
    GYF(1)=BB*GYF(1)
	GYF(2)=BB*GYF(2)
	TMP=(2.0D0*SIGMA(3))/ZZTT
	GYF(3)=BB*TMP*GYF(3)
	TMP=YVAL*TMP
    HYF(1,1)=dble(1.0e-12)+(YVAL*HYF(1,1)-Y2VAL*GYF(1)*GYF(1))/ATT
!    HYF(1,1)=(YVAL*HYF(1,1)-Y2VAL*GYF(1)*GYF(1))/ATT
	HYF(2,1)=(YVAL*HYF(2,1)-Y2VAL*GYF(1)*GYF(2))/ATT
	HYF(3,1)=(TMP*HYF(1,3)-Y2VAL*GYF(1)*GYF(3))/ATT
	HYF(1,2)=HYF(2,1)
	HYF(2,2)=dble(1.0e-12)+(YVAL*HYF(2,2)-Y2VAL*GYF(2)*GYF(2))/ATT
	HYF(3,2)=(TMP*HYF(2,3)-Y2VAL*GYF(2)*GYF(3))/ATT
	HYF(1,3)=HYF(3,1)
	HYF(2,3)=HYF(3,2)
	HYF(3,3)=dble(1.0e-12)+(2.0D0*YVAL*HYF(3,3)-Y2VAL*GYF(3)*GYF(3))/ATT 
	YF=ATT*ZYF
	
	RETURN
	END SUBROUTINE HYFUNCTION

!CC**************************** END OF PolyN IMPLEMENTATION
